import java.util.Properties
import java.io.FileInputStream

plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android" version "2.2.0"
    id "dev.flutter.flutter-gradle-plugin"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("key.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace = "com.manifestacion.grabovoi"
    compileSdk = 36
    ndkVersion = "26.1.10909125"

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
        coreLibraryDesugaringEnabled true
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    defaultConfig {
        applicationId = "com.manifestacion.grabovoi"
        minSdk = 21
        targetSdk = 36
        // IMPORTANTE: Incrementar versionCode en cada subida a Play Store. Debe ser MAYOR que el último en Play (ej. 39 → 40).
        versionCode = 47
        versionName = "2.3.34"
    }

    signingConfigs {
        release {
            if (!keystorePropertiesFile.exists()) {
                throw new IllegalStateException("No se encontró key.properties. Crea el keystore release y proporciona credenciales para firmar la app.")
            }

            if (!keystoreProperties.containsKey("storeFile")) {
                throw new IllegalStateException("key.properties no contiene 'storeFile'. Actualiza la configuración del keystore.")
            }

            storeFile = rootProject.file(keystoreProperties["storeFile"])
            storePassword = keystoreProperties["storePassword"]
            keyAlias = keystoreProperties["keyAlias"]
            keyPassword = keystoreProperties["keyPassword"]
        }
    }

    buildTypes {
        release {
            signingConfig = signingConfigs.getByName("release")
            minifyEnabled false  // Deshabilitado temporalmente para evitar problemas con FileProvider
            shrinkResources false
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro"
            )
        }
    }
}

flutter {
    source = "../.."
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.4'
    
    // Asegurar versión compatible de androidx.core para FileProvider
    implementation 'androidx.core:core:1.15.0'
    implementation 'androidx.core:core-ktx:1.15.0'
}